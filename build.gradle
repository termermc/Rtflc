plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.example'
version = '1.0'

jar {
    manifest {
        attributes 'Main-Class': 'net.termer.rtflc.Main'
    }
}

repositories {
    jcenter()
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:28.2-jre'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'

    // Application dependencies
    implementation 'net.lingala.zip4j:zip4j:2.5.0'
    implementation 'org.json:json:20190722'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'Rtflc'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Rtflc'
                description = 'Advanced and high(er) permformance Rtfl runtime and compiler'
                url = 'https://github.com/termermc/rtflc'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'termer'
                        name = 'Michael Termer'
                        email = 'termer@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/termermc/rtflc.git'
                    developerConnection = 'scm:git:ssh://github.com/termermc/rtflc.git'
                    url = 'https://github.com/termermc/rtflc'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}